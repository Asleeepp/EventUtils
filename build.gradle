plugins {
    id 'java'
    id "fabric-loom" version "1.6.9"
}

group = "cc.aabss"
version = "1.21-${project.version}"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://maven.terraformersmc.com/releases/' }
}

dependencies {
    minecraft("com.mojang:minecraft:1.21")
    mappings("net.fabricmc:yarn:1.21+build.1:v2")
    modImplementation("net.fabricmc:fabric-loader:0.15.11")
    modImplementation("net.fabricmc.fabric-api:fabric-api:0.100.1+1.21")
    modApi("me.shedaniel.cloth:cloth-config-fabric:15.0.127")
    modApi("com.terraformersmc:modmenu:11.0.0-beta.1")
    include(implementation("com.github.NepNep21:DiscordRPC4j16:1.2.2"))
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

def oldestJava = 17
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll(['-source', oldestJava.toString(), '-target', oldestJava.toString()])
}
